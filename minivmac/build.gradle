apply plugin: 'com.android.application'

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file('release.properties')))

android {
    compileSdkVersion 34

    defaultConfig {
        versionCode 49

        minSdkVersion 19
        targetSdkVersion 34
    }

    signingConfigs {
        release {
            storeFile file(props.keyStore)
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions = ['machine']
    productFlavors {
        macPlus {
            dimension 'machine'
            applicationId 'name.osher.gil.minivmac'
            versionName '1.6.2'

            ndk {
                moduleName 'mnvmcoreplus'
            }
        }

        macII {
            dimension 'machine'
            applicationId 'ninja.gil.miniv.ii'
            versionName '2.6.2'

            ndk {
                moduleName 'mnvmcoreii'
            }
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/Android.mk'
        }
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'name.osher.gil.minivmac'
}

dependencies {
    implementation(platform("org.jetbrains.kotlin:kotlin-bom:1.8.0"))

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.7.1'
    implementation 'androidx.navigation:navigation-ui:2.7.1'
    implementation 'androidx.preference:preference:1.2.1'
}
