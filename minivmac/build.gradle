plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android' version '1.8.22'
}

Properties props = new Properties()
props.load(new FileInputStream(rootProject.file('release.properties')))

android {
    compileSdkVersion 34

    defaultConfig {
        versionCode 51

        minSdkVersion 19
        targetSdkVersion 34

        multiDexEnabled true
    }

    signingConfigs {
        release {
            storeFile file(props.keyStore)
            storePassword props.keyStorePassword
            keyAlias props.keyAlias
            keyPassword props.keyAliasPassword
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.txt'
            signingConfig signingConfigs.release
        }
    }

    flavorDimensions = ['machine']
    productFlavors {
        macPlus {
            dimension 'machine'
            applicationId 'name.osher.gil.minivmac'
            versionName '1.6.4'

            ndk {
                moduleName 'mnvmcoreplus'
            }
        }

        macII {
            dimension 'machine'
            applicationId 'ninja.gil.miniv.ii'
            versionName '2.6.4'

            ndk {
                moduleName 'mnvmcoreii'
            }
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'x86', 'x86_64', 'armeabi-v7a', 'arm64-v8a'
            universalApk true
        }
    }

    externalNativeBuild {
        ndkBuild {
            path 'src/Android.mk'
        }
    }
    buildFeatures {
        viewBinding true
    }
    namespace 'name.osher.gil.minivmac'
}

dependencies {
    implementation 'com.android.support:multidex:1.0.3'

    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'

    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.7.1'
    implementation 'androidx.navigation:navigation-ui:2.7.1'
    implementation 'androidx.preference:preference:1.2.1'
}
